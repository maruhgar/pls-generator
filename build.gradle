apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'maven'
apply plugin: 'jetty'

// Change default directory to target from build

buildDir = 'target'

ext {
    springVersion = '4.0.1.RELEASE'
    securityVersion = '3.2.0.RELEASE'
    slf4jVersion = '1.7.6'
    logbackVersion = '1.1.1'
}

group = 'net.sourceforge.jukebox'
version = '1.1-SNAPSHOT'

repositories {
    // First check local cache before accessing central repository
    maven {
        url "file:///" + localRepoDir + "/.m2/repository"
    }
    mavenCentral()
}

configurations {
    provided
    testCompile.extendsFrom provided
    compile.transitive = true
}

dependencies {

    compile(
        [group: 'commons-configuration', name : 'commons-configuration', version : '1.6'],
        [group: 'javax.inject', name : 'javax.inject', version : '1'],
        [group: 'javax.validation', name : 'validation-api', version : '1.1.0.Final'],
        [group: 'org.hibernate', name : 'hibernate-validator', version : '5.0.1.Final'],
        [group: 'org.slf4j', name : 'slf4j-api', version : ext.slf4jVersion],
        [group: 'org.springframework', name : 'spring-core', version : ext.springVersion],
        [group: 'org.springframework', name : 'spring-context', version : ext.springVersion],
        [group: 'org.springframework', name : 'spring-context-support', version : ext.springVersion],
        [group: 'org.springframework', name : 'spring-web', version : ext.springVersion],
        [group: 'org.springframework', name : 'spring-webmvc', version : ext.springVersion],
        [group: 'org.springframework.security', name : 'spring-security-core', version : ext.securityVersion],
    )

    provided( 
        [group: 'javax.servlet', name : 'javax.servlet-api', version : '3.1.0'],
        [group: 'javax.el', name : 'javax.el-api', version : '3.0.0'],
        [group: 'org.glassfish', name : 'javax.el', version : '3.0.0']
    )

    testCompile(
        [group: 'org.mockito', name : 'mockito-core', version : '1.9.5'],
        [group: 'org.springframework', name : 'spring-test', version : ext.springVersion],
        [group: 'org.testng', name : 'testng', version : '6.1.1']
    )
    runtime( 
        [group: 'ch.qos.logback', name : 'logback-classic', version : ext.logbackVersion],
        [group: 'ch.qos.logback', name : 'logback-core', version : ext.logbackVersion],
        [group: 'org.freemarker', name : 'freemarker', version : '2.3.22'],
        [group: 'org.slf4j', name : 'jcl-over-slf4j', version : ext.slf4jVersion],
        [group: 'org.slf4j', name : 'jul-to-slf4j', version : ext.slf4jVersion],
        [group: 'org.slf4j', name : 'log4j-over-slf4j', version : ext.slf4jVersion],
        [group: 'org.springframework.security', name : 'spring-security-config', version : ext.securityVersion],
        [group: 'org.springframework.security', name : 'spring-security-web', version : ext.securityVersion]
    )
}

// Added the provided dependency to compile classpath
sourceSets.main.compileClasspath += configurations.provided

// Use TestNG instead of the default JUnit
test {
    useTestNG() 
}
